plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '6.25.0'
    id 'org.openapi.generator' version '7.17.0'
}

group = 'com.health'
version = '0.0.1-SNAPSHOT'
description = 'App for mental health '

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

ext{
    mapStructVersion = "1.6.3"
    springdocOpenapiUiVersion = "2.3.0"
    wiremockSpringBootVersion = "3.0.2"
    testContainersMongoVersion = "1.21.3"
    testContainersVersion = "2.0.2"
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-graphql'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocOpenapiUiVersion}"
    implementation "org.openapitools:jackson-databind-nullable:0.2.6"

    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.graphql:spring-graphql-test'
	testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "org.wiremock.integrations:wiremock-spring-boot:${wiremockSpringBootVersion}"
    testImplementation "org.testcontainers:mongodb:${testContainersMongoVersion}"
    testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
    testImplementation "org.testcontainers:testcontainers-junit-jupiter:${testContainersVersion}"

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

openApiGenerate {
    generatorName.set('spring')
    configOptions.set([
            title: "Mental Health App API",
            library: 'spring-boot',
            useTags: 'true',
            useJakartaEe: 'true'
    ])
    inputSpec.set("$projectDir/src/main/resources/mental-api.yml")
    modelPackage.set("com.health.generated.model")
    apiPackage.set("com.health.generated.api")
    outputDir.set(layout.buildDirectory.dir('openapi').get().toString())
    validateSpec.set(true)
}

sourceSets {
    main {
        java {
            srcDir file("${project.buildDir.path}/openapi/src/main/java")
        }
    }
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat()
    }
}

springBoot {
    mainClass = 'com.health.mental.MentalApplication'
}